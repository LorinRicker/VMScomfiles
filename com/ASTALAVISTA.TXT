From: Didier Morandi [Didier.Morandi@gmx.ch]
Sent: Wednesday, March 14, 2001 10:00 AM
To: Info-VAX@Mvb.Saic.Com
Subject: [DCL] minute of the day

ISLKP1_dmo> ty astalavista.com

$!+
$! ASTALAVISTA.COM
$!
$! how to build a single word index with DCL and find line numbered occurrences
$! This software is nothingware and the procedure may be deleted before use.
$!
$! V1.0-0 14-mar-2001 DMo     .com files only   (Didier.Morandi@Pachacamac.com)
$!-
$ set noon
$ say = "write sys$output"
$START:
$ if p1 .eqs. "" then inq p1 "Search or Build (S/B)"
$ if p1 .eqs. "" then exit
$ p1 = f$extract(0,1,f$edit(p1,"upcase"))
$ if p1 .nes. "S" .and. p1 .nes. "B"
$ then
$    p1 = ""
$    goto START
$ endif
$ if p1 .eqs. "S" then goto DO_SEARCH
$ if p2 .eqs. "" then inq p2 "disk:directory or [] ([*...] allowed)"
$ if f$search("thesaurus.dat") .nes. ""
$ then
$    say ""
$    say "A previous THESAURUS.DAT file exists in ",f$dir()
$    inq ok "Do you want to build a new one [Y/N]?"
$    if .not. ok then exit
$ endif
$ if p2 .eqs. "" then exit
$ skip_table = ".and..eq..eqs..gt..ge..ges..gts..le..les..lt..ne..nes..or."
$ skip_table = skip_table + """&'()-|_@[]$*,;:!<ESC>^G"
$ skip_table = skip_table + "ifthenelseendifgosubgotoreturnexit$deck$eod"
$ if f$search("dir.temp") .nes. "" then dele_ dir.temp;*
$ dire_/col=1/notrail/noheader/out=dir.temp/exclude=[vms$common...] 'p2'*.com;
$ close/nolog ch
$ close/nolog ch2
$ close/nolog ch3
$ nbf = 0
$ nbw = 0
$ start_time = f$time()
$ open/read ch dir.temp
$ if f$search("temp$temp.not_sorted").nes."" then dele_ temp$temp.not_sorted.*
$ open/write ch3 temp$temp.not_sorted
$LOOP:
$ read/end=EOF ch file
$ file = f$edit(file,"lowercase")
$ say "processing ",file
$ nbf = nbf + 1
$ open/read ch2 'file'
$LOOP2:
$ read/end=EOF2 ch2 line
$ line=f$edit(line,"trim,compress,lowercase,uncomment")
$ if line .eqs. "$" .or. line .eqs. "$ " .or. line .eqs. "$!" then goto LOOP2
$ if f$locate(" ",line) .eq. f$len(line)
$ then
$    line = line - "'" - "'" - "'" - "," - """" - """" - "<ESC>" - "^G"
$    line = line - "'" - "'" - "'" - "," - """" - """" - "<ESC>" - "^G"
$    if f$extract(f$len(line)-1,1,line) .nes. ":" then -
        write ch3 line," ",file
$    goto LOOP2
$ endif
$ i=0
$LOOP3:
$ word = f$element(i," ",line)
$ word = word - "'" - "'" - "'" - "," - """" - """" - "<ESC>" - "^G"
$ word = word - "'" - "'" - "'" - "," - """" - """" - "<ESC>" - "^G"
$ if word .eqs. " " .or. word .eqs. "" then goto LOOP2
$ i=i+1
$ if f$extract(f$len(word)-1,1,word) .eqs. ":" then goto LOOP3  !skip labels
$ if f$locate(word,skip_table) .ne. f$len(skip_table) then goto LOOP3
$ if f$extract(0,2,word) .eqs. "$!" then goto LOOP3             !skip comments
$ write ch3 f$edit(word,"trim,compress,uncomment,lowercase")," ",file
$ nbw = nbw + 1
$ goto LOOP3
$EOF2:
$ close ch2
$ goto LOOP
$EOF:
$ close ch
$ close ch3
$!+
$! NB If you build the system disk thesaurus, log in under the system account
$! or you will run out of virtual memory *now*
$!-
$ sort/key=(pos:1,size:20)        temp$temp.not_sorted temp$temp.sorted
$ merge/key=(pos:1,size:20)/nodup temp$temp.sorted     thesaurus.dat
$ dele_ temp$temp.not_sorted;*, temp$temp.sorted;*, dir.temp;*
$ say ""
$ say "starting time was ",start_time
$ say "ending   time is  ",f$time()
$ say nbw," words stored from ",nbf," files in ",p2,"*.com"
$ say ""
$ exit
$!
$DO_SEARCH:
$ if p2 .eqs. "" then inq p2 -
  "Item to search (enclose more than one with double quotes)"
$ if p2 .eqs. "" then exit
$ if f$search("search.temp") .nes. "" then dele_ search.temp;*
$ search = "search"
$ search/output=search.temp thesaurus.dat "''p2' "
$ if $severity .eqs. "1"
$ then
$    close/nolog ch
$    open/read ch search.temp
$LOOP_S:
$    read/end=EOF_S ch line
$    file = f$element(1," ",line)
$    if file .eqs. " " .or. file .eqs. "" then goto LOOP_S
$    say "Occurrences found in ",file
$    search/numbers 'file' "''p2'"
$    goto LOOP_S
$EOF_S:
$    close ch
$ endif
$ dele_ search.temp;*
$ exit

ISLKP1_dmo> @astalavista b sys$sysdevice:[*...]

starting time was 14-MAR-2001 16:36:20.13
ending   time is  14-MAR-2001 16:45:24.85
660907 words stored from 1699 files in SYS$SYSDEVICE:[*...]*.com

ISLKP1_dmo> @astalavista s dmo   
Occurrences found in sys$sysdevice:[vms$common.sysexe]shutdown.com;1
   882  $dmo = "dismount /abort /override=checks "
   885  $dmo = dmo + " /unload"
   887  $dmo = dmo + " /nounload"
   928  $dmo 'cldmt' 'dev'
Occurrences found in sys$sysdevice:[sys0.syscommon.sysexe]shutdown.com;1
   882  $dmo = "dismount /abort /override=checks "
   885  $dmo = dmo + " /unload"
   887  $dmo = dmo + " /nounload"
   928  $dmo 'cldmt' 'dev'

ISLKP1_dmo> dir thesaurus.dat

Directory SYS$SYSDEVICE:[MORANDI]

THESAURUS.DAT;14                          6946/96138   14-MAR-2001 16:50:48.45

Total of 2 files, 6946/96138 blocks.
(it took me 16:50:48.45 - 16:45:24.85 minutes to log in under the system account
and redo the sort/merge by hand :-)

ISLKP1_dmo> cop thesaurus.dat nl:/log
%COPY-S-COPIED, SYS$SYSDEVICE:[MORANDI]THESAURUS.DAT;14 copied to NL:
(46321 records)

Now, your question is: what is the difference with
$ search/numbers sys$sysdevice:[*...]*.com item

The answer is: if there is no occurrences, I know it at once, and it is *much*
faster as I search only the files in which I already know that the occurence is

D.
(is there an AltaVista Personal Search for VMS? :-)
-- 
MORANDI Consultants, Swiss Quality Computer Consulting
avenue de Granges-Paccot 2, 1700 Fribourg  Switzerland
    Tel: +41.79.705.46.70 - Fax: +41.26.465.13.58
 Visit our Web site at http://Didier.Morandi.Free.fr
